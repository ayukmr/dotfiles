#!/usr/bin/env zsh

# ==========================
# === Pager for Newsboat ===
# ==========================

function spinner {
  local pid
  local spin
  local back
  local frame

  pid=$!
  spin=(⠋ ⠙ ⠹ ⠸ ⠼ ⠴ ⠦ ⠧ ⠇ ⠏)

  width="$(( $(tput cols) - 18 ))"

  # hide cursor
  echo -n '\e[2J\e[1;1H\e[?25l' > /dev/stderr

  # show first frame of spinner
  print -nP "%B%K{magenta}%F{black} ${spin[0]} loading article" > /dev/stderr
  jot -n -s '' -b ' ' $width > /dev/stderr
  print -nP "%f%k%b" > /dev/stderr

  while kill -0 $pid &> /dev/null; do
    # show spinner
    for frame in "${spin[@]}"; do
      print -nP "\r%B%K{magenta}%F{black} $frame loading article" > /dev/stderr
      jot -n -s '' -b ' ' $width > /dev/stderr
      print -nP "%f%k%b" > /dev/stderr

      sleep 0.1
    done
  done

  # show cursor
  echo -n '\e[?25h' > /dev/stderr
}

# remove header
body="$(awk 'BEGIN {header=1} header && NF==0 {header=0} !header {print $0}' "$1")"

# convert html to markdown
markdown="$(node ~/.cfg/newsboat/turndown-cli/index.js <<< "$body" & spinner)"

# remove blank code blocks
sed -E 's/^(> )+$//' <<< "$markdown" |

# format markdown for viewing
glow -w 175 -s ~/.cfg/glow/style.json - |

# page output
less -cR --tilde
