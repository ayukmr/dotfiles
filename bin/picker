#!/usr/bin/env osascript

-- ==================
-- === Pick Color ===
-- ==================

-- convert rgb to hexadecimal
on convertRGBColorToHexValue(RGBValues)
    set hexList to {"0", "1", "2", "3", "4", "5", "6", "7", "8", "9", "a", "b", "c", "d", "e", "f"}
    set hexValue to ""

    repeat with a from 1 to count of RGBValues
        set currentRGBValue to (item a of RGBValues) div 256
        if currentRGBValue is 256 then set currentRGBValue to 255

        set firstItem to item ((currentRGBValue div 16) + 1) of hexList
        set secondItem to item (((currentRGBValue / 16 mod 1) * 16) + 1) of hexList

        set hexValue to (hexValue & firstItem & secondItem) as string
    end repeat

    return ("#" & hexValue) as string
end convertRGBColorToHexValue

-- show color picker for input
set hexColor to (convertRGBColorToHexValue(choose color))

display alert "Color: " & hexColor buttons {"Don't Copy", "Copy"} default button "Copy"

-- copy hexadecimal color to clipboard
if the button returned of the result is "Copy" then
    set the clipboard to hexColor
end if
